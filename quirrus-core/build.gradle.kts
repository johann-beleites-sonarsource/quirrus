import com.expediagroup.graphql.plugin.gradle.config.GraphQLScalar
import com.expediagroup.graphql.plugin.gradle.config.GraphQLSerializer
import com.expediagroup.graphql.plugin.gradle.tasks.GraphQLGenerateClientTask
import com.expediagroup.graphql.plugin.gradle.tasks.GraphQLIntrospectSchemaTask

plugins {
    java
    kotlin("jvm")
    kotlin("plugin.serialization")

    id("org.jetbrains.compose")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.openjfx.javafxplugin")
    id("com.expediagroup.graphql") version "8.0.0"
}

val javaTarget: String by project

repositories {
    google()
    maven("https://maven.pkg.jetbrains.space/public/p/compose/dev")
    jcenter()
}

dependencies {
    val ktor_version: String by project

    implementation(compose.desktop.currentOs)

    implementation(kotlin("stdlib-jdk8"))

    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-cio:$ktor_version")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")
    implementation("io.ktor:ktor-client-okhttp-jvm:$ktor_version")
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")

    implementation("me.lazmaid.kraph:kraph:0.6.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2")
    implementation("com.github.ajalt.clikt:clikt:3.4.0")

    implementation("com.expediagroup:graphql-kotlin-ktor-client:8.0.0")
}

javafx {
    version = javaTarget
    modules = listOf("javafx.controls", "javafx.swing", "javafx.web", "javafx.graphics")
}

val graphqlIntrospectSchema by tasks.getting(GraphQLIntrospectSchemaTask::class) {
    endpoint.set("https://api.cirrus-ci.com/graphql")
    outputFile.set(file("src/main/resources/schema.graphql"))
    headers.set(mapOf("Cookie" to (System.getenv()["CIRRUS_COOKIE"] ?: "")))
}

val graphqlGenerateClient by tasks.getting(GraphQLGenerateClientTask::class) {
    // we need to overwrite default behavior of using Jackson data model
    serializer.set(GraphQLSerializer.KOTLINX)
    packageName.set("org.sonarsource.dev.quirrus.generated.graphql")
    // use schema file generated by the introspection task
    schemaFile.set(graphqlIntrospectSchema.outputFile)
    // make sure to run client generation after introspection task
    dependsOn("graphqlIntrospectSchema")

    // generate client for all queries and mutations
    queryFileDirectory.set(file("src/main/resources/queries"))
    //queryFiles.from(fileTree("src/main/resources/queries"))
}
